#!/bin/csh
# Script to check the inter-camera frame timings between two previously generated frame timings provided by
# moptop_analyse_frame_timings
if ( $#argv != 2 ) then
   echo "moptop_analyse_frame_timings <camera1 frame timings file> <camera 2 frame timings file>"
   echo "The frame timings file should have been generated by moptop_analyse_frame_timings."
   exit 1
endif
set this_dir = `dirname $0`
#echo "this dir = ${this_dir}"
# find date_to_ms
if ( -x /home/dev/bin/ngatastro/test/${HOSTTYPE}/date_to_ms ) then
   alias date_to_ms "/home/dev/bin/ngatastro/test/${HOSTTYPE}/date_to_ms"
else if ( -x /icc/bin/ngatastro/test/${HOSTTYPE}/date_to_ms ) then
   alias date_to_ms "/icc/bin/ngatastro/test/${HOSTTYPE}/date_to_ms"
else
   echo "Failed to find date_to_ms."
endif
#alias | grep date_to_ms
set camera1_frame_timings_file = $1
set camera2_frame_timings_file = $2
set camera1_cam_time_file = ${camera1_frame_timings_file:r}_camtime.txt
set camera2_cam_time_file = ${camera2_frame_timings_file:r}_camtime.txt
grep CAMTIME ${camera1_frame_timings_file} > ${camera1_cam_time_file}
grep CAMTIME ${camera2_frame_timings_file} > ${camera2_cam_time_file}
set camtime1_count = `wc -l ${camera1_cam_time_file} | awk ' { print $1}'`
set camtime2_count = `wc -l ${camera1_cam_time_file} | awk ' { print $1}'`
echo "camtime1_count = ${camtime1_count} : camtime2_count = ${camtime2_count}"
if( ${camtime1_count} != ${camtime2_count} ) then
    echo "Number of CAMTIME entries differ: ${camtime1_count} != ${camtime2_count}."
    exit 2
endif
set index = 1
set last_index = `echo " ( ${camtime1_count} + 1 )" | bc`
while ( ${index} < ${last_index} )
      set camtime_line1 = `${this_dir}/print_line.awk -v LINE=${index} ${camera1_cam_time_file}`
      set camtime_line2 = `${this_dir}/print_line.awk -v LINE=${index} ${camera2_cam_time_file}`
      set camtime1 = `echo ${camtime_line1} | sed 's/CAMTIME Index .* : The difference between \(.*\) and .*/\1/g'`
      set camtime2 = `echo ${camtime_line2} | sed 's/CAMTIME Index .* : The difference between \(.*\) and .*/\1/g'`
      set camtime1_ms = `date_to_ms ${camtime1}`
      set camtime2_ms = `date_to_ms ${camtime2}`
      set camtime_offset_ms = `echo "( ${camtime1_ms} - ${camtime2_ms} )" | /usr/bin/bc`
      echo "Index ${index} : CAMTIME1 ${camtime1} : CAMTIME2 ${camtime2} : Difference ${camtime_offset_ms}"
      @ index ++
end
